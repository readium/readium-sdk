apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def ndk_skipX86 = (rootProject.hasProperty("readium_ndk_skipX86") && rootProject.readium_ndk_skipX86)
def ndk_skipARM = (rootProject.hasProperty("readium_ndk_skipARM") && rootProject.readium_ndk_skipARM)
def ndk_clang = (rootProject.hasProperty("readium_ndk_clang") && rootProject.readium_ndk_clang)
def ndk_experimental = (rootProject.hasProperty("readium_ndk_experimental") && rootProject.readium_ndk_experimental)

android {
    compileSdkVersion = 26
    buildToolsVersion = "27.0.2"

    defaultConfig {
        // manifest@package attribute in src/main/AndroidManifest.xml
        // applicationId = "org.readium.sdk.android"

        // manifest/uses-sdk@android:minSdkVersion attribute in src/main/AndroidManifest.xml
        minSdkVersion 19

        // manifest/uses-sdk@android:targetSdkVersion attribute in src/main/AndroidManifest.xml
        targetSdkVersion 26

        // manifest@android:versionCode attribute in src/main/AndroidManifest.xml
        versionCode 29

        // manifest@android:versionName attribute in src/main/AndroidManifest.xml
        versionName "1.29"
    }

    sourceSets {
        main {
            java {
                srcDirs = [ './src/main/java' ]
            }

            jniLibs {
                dependencies {
                    project ":epub3"
                }
                srcDirs = [ '../epub3/libs' ]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled = false
        }
        release {
            minifyEnabled = false
        }
    }

    productFlavors {
        if (!ndk_skipARM)
            "arm" {
                ndk.with {
                    abiFilters = ["armeabi-v7a"]
                }
            }

        if (!ndk_skipX86)
            "x86" {
                ndk.with {
                    abiFilters = ["x86"]
                }
            }
    }
}

Properties properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

ext {
    // Libraries
    bintray_user = properties.getProperty('bintray.user', "")
    bintray_api_key = properties.getProperty('bintray.apikey', "")
    groupId = "fr.youboox"
    packageRepo = "maven"
    packageName = "readium-sdk-android"
    packageOrganization = "youboox"
    libraryVersion = "0.29.0"
    libraryDesc = "Readium SDK (Android API)"
    siteUrl = 'https://github.com/readium/readium-sdk'
    gitUrl = 'https://github.com/readium/readium-sdk.git'
}

apply from: 'publish.gradle'